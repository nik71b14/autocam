#version 460
#define WORKGROUP_SIZE 1024
layout(local_size_x = WORKGROUP_SIZE) in;

layout(std430, binding = 0) readonly buffer Counts {
    uint counts[];
};

layout(std430, binding = 1) writeonly buffer PrefixSums {
    uint prefix[];
};

layout(std430, binding = 2) writeonly buffer BlockSums {
    uint blockSums[];
};

uniform uint numBlocks;
uniform uint numElements;

shared uint temp[WORKGROUP_SIZE];

void main() {
    uint gid = gl_GlobalInvocationID.x;
    uint lid = gl_LocalInvocationID.x;
    uint group = gl_WorkGroupID.x;

    // Check if the global ID is within bounds
    if (gid < numElements)
        temp[lid] = counts[gid];
    else
        temp[lid] = 0;

    // Load input to shared memory with zero padding
    temp[lid] = (gid < counts.length()) ? counts[gid] : 0;

    memoryBarrierShared();
    barrier();

    // Inclusive scan (Kogge-Stone)
    for (uint offset = 1; offset < WORKGROUP_SIZE; offset *= 2) {
        uint val = 0;
        if (lid >= offset)
            val = temp[lid - offset];
        barrier();
        temp[lid] += val;
        barrier();
    }

    // Write block sum (last thread of group)
    if (lid == WORKGROUP_SIZE - 1 && group < numBlocks)
        blockSums[group] = temp[lid];

    // Exclusive scan result
    uint scanned = (lid > 0) ? temp[lid - 1] : 0;

    if (gid < prefix.length())
        prefix[gid] = scanned;
}
